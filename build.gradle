buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "0.3"
group= "gliby.minecraft" 
archivesBaseName = "glibysphysics"

minecraft {
    version = "1.8-11.14.4.1577"
    runDir = "run"
    mappings = "snapshot_20141130"
    
    makeObfSourceJar = false 

    replace '@VERSION@', project.version
    replace '@MC_VERSION@', version
}


processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    classifier = ''
    relocate "org.apache.commons.math3", "gliby.repackage.org.apache.commons.math3"
    relocate "com.badlogic", "com.badlogic"
    relocate "com.bulletphysicsx", "gliby.repackage.com.bulletphysicsx"
    configurations = [project.configurations.shadow]
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
}

dependencies {
    shadow group: 'org.apache.commons', name: 'commons-math3', version: '3.0'
    shadow group: 'javax.vecmath', name: 'vecmath', version: '1.5.2'
    shadow files('lib/gdx-bullet.jar')
    shadow files('lib/gdx-bullet-natives.jar')
    shadow files('lib/gdx.jar')
}

jar {
    manifest {
        attributes 'FMLAT' : 'GlibysPhysics_at.cfg'
    }
}
